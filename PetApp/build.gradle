apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

import java.util.regex.Pattern

android {
    namespace 'com.demo.pet.petapp'
    compileSdk 34

    defaultConfig {
        applicationId "com.demo.pet.petapp"
        minSdkVersion 29
        targetSdk 34
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix = '.debug'
            versionNameSuffix = '-debug'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    productFlavors {
    }

    configurations.configureEach {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
        resolutionStrategy.force "com.android.support:support-annotations:25.4.0"
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/INDEX.LIST', 'META-INF/DEPENDENCIES']
        }
    }

    // Define APK name and replace.
    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            if (output.outputFileName.endsWith(".apk")) {
                def name = getArchivesBaseName()
                def ver = variant.versionName

                switch (variant.name) {
                    case "debug":
                        // NOP.
                        break

                    case "release":
                        output.outputFileName = "${name}_${ver}.apk"
                        break
                }
            }
        }
    }

    ndkVersion '27.0.12077973'

    incrementVersionCode()
}

def incrementVersionCode() {
    def file = file("src/main/AndroidManifest.xml")
    def text = file.getText()

    // Update version code.
    def codePattern = Pattern.compile("android:versionCode=\"(\\d+)\"")
    def codeMatcher = codePattern.matcher(text)
    codeMatcher.find()
    def code = Integer.parseInt(codeMatcher.group(1))
    def newCode = code + 1
    def codeUpdatedText = codeMatcher.replaceAll("android:versionCode=\"" + newCode + "\"")

    // Update version name.
    def namePattern = Pattern.compile("android:versionName=\"(\\d+)\\.(\\d+)\\.(\\d+)-\\d+\"")
    def nameMatcher = namePattern.matcher(codeUpdatedText)
    nameMatcher.find()
    def major = Integer.parseInt(nameMatcher.group(1))
    def minor = Integer.parseInt(nameMatcher.group(2))
    def patch = Integer.parseInt(nameMatcher.group(3))
    def newName = "android:versionName=\"" + major + "." + minor + "." + patch + "-" + newCode + "\""
    def nameUpdatedText = nameMatcher.replaceAll(newName)

    file.write(nameUpdatedText)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.17.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.activity:activity-ktx:1.9.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // Local library or pocket-sphinx.
    implementation project(':pocketsphinx-aar')
    implementation project(':pocketsphinx-models')

    // Network.
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'

    // gRPC for Google Cloud API.
    implementation 'io.grpc:grpc-okhttp:1.66.0'

    // Google Cloud API.
    implementation 'com.google.cloud:google-cloud-speech:4.42.0'
}

tasks.register('copySecretKey', Copy) {
    File secretKey = file "$System.env.GOOGLE_APPLICATION_CREDENTIALS"
    from secretKey.getParent()
    include secretKey.getName()
    into 'src/main/res/raw'
    rename secretKey.getName(), "credential.json"
}
preBuild.dependsOn(copySecretKey)
